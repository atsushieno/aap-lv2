plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'maven-publish'
}

android {
    compileSdkVersion 30
    defaultConfig {
        minSdkVersion 29
        targetSdkVersion 30
        versionCode 0
        versionName "0.1"

        externalNativeBuild {
            cmake {
                // Enable this section if you want to debug native code using AddressSanitizer
                //arguments "-DANDROID_ARM_MODE=arm", "-DANDROID_STL=c++_shared"
                //cppFlags "-fsanitize=address -fno-omit-frame-pointer"
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    externalNativeBuild {
        cmake {
            path "src/main/cpp/CMakeLists.txt"
        }
    }

    packagingOptions {
        // FIXME: (ASAN) looks like a hack, but -DANDROID_STL=c++_shared causes this https://stackoverflow.com/questions/48749876/more-than-one-file-was-found-with-os-independent-path-lib-armeabi-v7a-libopencv
        pickFirst '*.so'
        doNotStrip "*/*/*.so" // debuggable!
    }
}

afterEvaluate {
    publishing {
        publications {
            // Creates a Maven publication called "release".
            release(MavenPublication) {
                // Applies the component for the release build variant.
                groupId = 'org.androidaudioplugin'
                version = android.defaultConfig.versionName
                from components.release
                artifactId = 'androidaudioplugin-lv2'
            }
            // Creates a Maven publication called "debug".
            debug(MavenPublication) {
                // Applies the component for the release build variant.
                groupId = 'org.androidaudioplugin'
                version = android.defaultConfig.versionName
                from components.debug
                artifactId = 'androidaudioplugin-lv2'
            }
        }
        repositories {
            maven {
                def releasesRepoUrl = "$buildDir/repos/releases"
                def snapshotsRepoUrl = "$buildDir/repos/snapshots"
                url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            }
        }
    }
}

dependencies {
    implementation project(':androidaudioplugin')
    implementation 'androidx.core:core-ktx:1.3.2'
    testImplementation 'junit:junit:4.13.1'
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'
}
