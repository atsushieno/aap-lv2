plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'maven-publish'
}

android {
    compileSdkVersion 30
    defaultConfig {
        minSdkVersion 29
        targetSdkVersion 30
        versionCode 0
        versionName "0.1.12"

        externalNativeBuild {
            cmake {
                // https://github.com/google/prefab/blob/bccf5a6a75b67add30afbb6d4f7a7c50081d2d86/api/src/main/kotlin/com/google/prefab/api/Android.kt#L243
                arguments "-DANDROID=1", "-DANDROID_STL=c++_shared"

                // Enable this section if you want to debug native code using AddressSanitizer
                //arguments "-DANDROID_ARM_MODE=arm", "-DANDROID_STL=c++_shared"
                //cppFlags "-fsanitize=address -fno-omit-frame-pointer"
            }
        }
    }
    // See android-audio-plugin-framework/java/androidaudioplugin/build.gradle to find out why it is required...
    ndkVersion "21.3.6528147"

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    // FIXME: enable these sections once we migrated to prefab.
    /*
    buildFeatures {
        prefab true
        prefabPublishing true
    }
    // Workaround for https://issuetracker.google.com/issues/168777344#comment5
    packagingOptions {
        exclude '**.so'
    }
    prefab {
        androidaudioplugin-lv2 {
            name 'androidaudioplugin-lv2'
        }
    }
    */
    packagingOptions {
        exclude '**libc++_shared.so'
    }

    externalNativeBuild {
        cmake {
            path "src/main/cpp/CMakeLists.txt"
        }
    }
}

afterEvaluate {
    publishing {
        publications {
            // Creates a Maven publication called "release".
            release(MavenPublication) {
                // Applies the component for the release build variant.
                // FIXME: release build doesn't work. See https://github.com/atsushieno/android-audio-plugin-framework/issues/68 for details.
                groupId = 'org.androidaudioplugin'
                version = android.defaultConfig.versionName
                from components.debug
                artifactId = 'androidaudioplugin-lv2'
            }
            // Creates a Maven publication called "debug".
            debug(MavenPublication) {
                // Applies the component for the debug build variant.
                groupId = 'org.androidaudioplugin'
                version = android.defaultConfig.versionName
                from components.debug
                artifactId = 'androidaudioplugin-lv2'
            }
        }
        repositories {
            maven {
                def releasesRepoUrl = "$buildDir/repos/releases"
                def snapshotsRepoUrl = "$buildDir/repos/snapshots"
                url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            }
        }
    }
}

dependencies {
    implementation 'org.androidaudioplugin:androidaudioplugin:0.6.10'
    implementation 'androidx.core:core-ktx:1.3.2'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'
}
