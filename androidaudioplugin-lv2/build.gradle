plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'maven-publish'
}

android {
    compileSdkVersion 33
    defaultConfig {
        minSdkVersion 29
        targetSdkVersion 33
        versionCode 1
        versionName "$aap_lv2_version"

        externalNativeBuild {
            cmake {
                // https://github.com/google/prefab/blob/bccf5a6a75b67add30afbb6d4f7a7c50081d2d86/api/src/main/kotlin/com/google/prefab/api/Android.kt#L243
                arguments "-DANDROID=1", "-DANDROID_STL=c++_shared", "-DBUILD_WITH_PREFAB=1", "-DAAP_ENABLE_ASAN=" + (enable_asan ? "1" : "0")
            }
        }
    }
    // See android-audio-plugin-framework/java/androidaudioplugin/build.gradle to find out why it is required...
    ndkVersion "$ndk_version"

    buildTypes {
        debug {
            packagingOptions {
                doNotStrip "**/*.so"
            }
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    // https://github.com/google/prefab/issues/127
    packagingOptions {
        exclude '**/libc++_shared.so'
        exclude '**/libandroidaudioplugin.so'
    }

    buildFeatures {
        prefab true
        prefabPublishing true
    }
    prefab {
        create("androidaudioplugin-lv2") {
            name 'androidaudioplugin-lv2'
        }
    }

    externalNativeBuild {
        cmake {
            version "3.18.1"
            path "src/main/cpp/CMakeLists.txt"
        }
    }
}

afterEvaluate {
    publishing {
        publications {
            // Applies the component for the release build variant.
            // FIXME: release build doesn't work. See https://github.com/atsushieno/android-audio-plugin-framework/issues/68 for details.
            release(MavenPublication) {
                version = android.defaultConfig.versionName
                from components.release
            }
            debug(MavenPublication) {
                version = android.defaultConfig.versionName
                from components.debug
            }
        }
    }
}

dependencies {
    implementation project(":androidaudioplugin")
    implementation 'androidx.core:core-ktx:1.9.0'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
}
