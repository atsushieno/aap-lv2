plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'maven-publish'
    id 'org.jetbrains.dokka'
    id 'signing'
}

android {
    namespace 'org.androidaudioplugin.lv2'

    defaultConfig {
        compileSdkPreview = "VanillaIceCream"
        minSdk = 29
        versionCode 1
        versionName libs.versions.aap.lv2.get()

        externalNativeBuild {
            cmake {
                // https://github.com/google/prefab/blob/bccf5a6a75b67add30afbb6d4f7a7c50081d2d86/api/src/main/kotlin/com/google/prefab/api/Android.kt#L243
                arguments "-DANDROID=1", "-DANDROID_STL=c++_shared", "-DBUILD_WITH_PREFAB=1", "-DAAP_ENABLE_ASAN=" + (enable_asan ? "1" : "0")
            }
        }
    }
    // See aap-core/androidaudioplugin/build.gradle.kts to find out why it is required...
    ndkVersion libs.versions.ndk.get()

    buildTypes {
        debug {
            packagingOptions {
                doNotStrip "**/*.so"
            }
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    // https://github.com/google/prefab/issues/127
    packagingOptions {
        jniLibs {
            excludes += ['**/libc++_shared.so', '**/libandroidaudioplugin.so']
        }
    }

    buildFeatures {
        prefab true
        prefabPublishing true
    }
    prefab {
        create("androidaudioplugin-lv2") {
            name 'androidaudioplugin-lv2'
        }
    }

    externalNativeBuild {
        cmake {
            version libs.versions.cmake.get()
            path "src/main/cpp/CMakeLists.txt"
        }
    }

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_1_8
        targetCompatibility = JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
}

dependencies {
    implementation (libs.aap.core)
    //  If you want to test aap-core locally, switch to these local references
    //  (along with settings.gradle.kts changes)
    //implementation (project(":androidaudioplugin"))

    implementation (libs.androidx.core.ktx)

    testImplementation (libs.junit)
    androidTestImplementation (libs.test.ext.junit)
    androidTestImplementation (libs.test.espresso.core)
}

apply from: "../publish-pom.gradle"
