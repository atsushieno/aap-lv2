android {
    publishing {
        multipleVariants {
            allVariants()
            withJavadocJar()
            withSourcesJar()
        }
    }
}

afterEvaluate {

    tasks.register('javadocJar', Jar) {
        archiveClassifier.set('javadoc')
    }
    tasks.register('sourceJar', Jar) {
        from android.sourceSets.main.java.srcDirs
        from android.sourceSets.main.kotlin.srcDirs
        archiveClassifier.set('sources')
    }

    publishing {
        repositories {
            maven {
                name = "OSSRH"
                url = uri("https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/")
                credentials {
                    username = System.getenv("OSSRH_USERNAME")
                    password = System.getenv("OSSRH_PASSWORD")
                }
            }
        }

        publications {
            release(MavenPublication) {
                version = android.defaultConfig.versionName
                from components.release
            }

            androidaudioplugin_lv2(MavenPublication) {
                from components.findByName("androidaudioplugin-lv2")

                artifact(javadocJar)
                artifact(sourceJar)

                pom {
                    name.set(project.name)
                    description.set("AndroidAudioPlugin - LV2")
                    url.set("https://github.com/atsushieno/aap-lv2")
                    scm {
                        url.set("https://github.com/atsushieno/aap-lv2")
                    }
                    licenses {
                        license {
                            name.set("the MIT License")
                            url.set("https://github.com/atsushieno/aap-lv2/blob/main/LICENSE")
                        }
                    }
                    developers {
                        developer {
                            id.set("atsushieno")
                            name.set("Atsushi Eno")
                            email.set("atsushieno@gmail.com")
                        }
                    }
                }
            }
        }
    }

    // keep it as is. It is replaced by CI release builds
    signing {}
}
