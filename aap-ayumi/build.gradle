
plugins {
    id 'com.android.application'
    id 'kotlin-android'
}

android {
    compileSdkVersion 33

    defaultConfig {
        applicationId "org.androidaudioplugin.aap_ayumi"
        minSdkVersion 29
        targetSdkVersion 33
        versionCode 1
        versionName "$aap_lv2_version"

        externalNativeBuild {
            cmake {
                // https://github.com/google/prefab/blob/bccf5a6a75b67add30afbb6d4f7a7c50081d2d86/api/src/main/kotlin/com/google/prefab/api/Android.kt#L243
                arguments "-DANDROID_STL=c++_shared", "-DAAP_ENABLE_ASAN=" + (enable_asan ? "1" : "0")
            }
        }

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    ndkVersion "$ndk_version"

    buildTypes {
        debug {
            packagingOptions {
                doNotStrip "**/*.so"
            }
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    buildFeatures {
        prefab true
    }

    externalNativeBuild {
        cmake {
            version "3.18.1"
            path "src/main/cpp/CMakeLists.txt"
        }
    }

    packagingOptions {
        if (enable_asan)
            jniLibs.useLegacyPackaging = true
    }
}

dependencies {
    implementation project(':androidaudioplugin-lv2')
    implementation "org.androidaudioplugin:androidaudioplugin:$aap_version"
    implementation "org.androidaudioplugin:androidaudioplugin-ui-compose:$aap_version"
    implementation "org.androidaudioplugin:androidaudioplugin-midi-device-service:$aap_version"
    implementation 'com.google.android.material:material:1.7.0'

    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation "org.androidaudioplugin:androidaudioplugin-testing:$aap_version"
    androidTestImplementation 'androidx.test:rules:1.4.0'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
}

// Starting AGP 7.0.0-alpha05, AGP stopped caring build dependencies and it broke builds.
// This is a forcible workarounds to build libandroidaudioplugin.so in prior to referencing it.
gradle.projectsEvaluated {
    tasks['buildCMakeDebug'].dependsOn(rootProject.project("androidaudioplugin-lv2").mergeDebugNativeLibs)
    tasks['buildCMakeRelWithDebInfo'].dependsOn(rootProject.project("androidaudioplugin-lv2").mergeReleaseNativeLibs)
}
