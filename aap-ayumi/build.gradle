
plugins {
    id 'com.android.application'
    id 'kotlin-android'
}

android {
    compileSdkVersion 30

    defaultConfig {
        applicationId "org.androidaudioplugin.aap_ayumi"
        minSdkVersion 29
        targetSdkVersion 30
        versionCode 1
        versionName "0.1.15"

        externalNativeBuild {
            cmake {
                // https://github.com/google/prefab/blob/bccf5a6a75b67add30afbb6d4f7a7c50081d2d86/api/src/main/kotlin/com/google/prefab/api/Android.kt#L243
                arguments "-DANDROID_STL=c++_shared"

                // Enable this section if you want to debug native code using AddressSanitizer
                //arguments "-DANDROID_ARM_MODE=arm", "-DANDROID_STL=c++_shared"
                //cppFlags "-fsanitize=address -fno-omit-frame-pointer"
            }
        }

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        debug {
            packagingOptions {
                doNotStrip "**/*.so"
            }
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    // FIXME: enable these sections once we migrated to prefab.
    /*
    buildFeatures {
        prefab true
    }
    */

    externalNativeBuild {
        cmake {
            version "3.18.1"
            path "src/main/CMakeLists.txt"
        }
    }

    // AGP, Android Lint, and/or appcompat package is stupid and results in their own error to block our builds.
    // > androidx.appcompat.AppCompatIssueRegistry in /home/runner/.gradle/caches/transforms-2/files-2.1/2dfc7e2f9c4e61e976acb8f06b4a9014/appcompat-1.2.0/jars/lint.jar does not specify a vendor; see IssueRegistry#vendor
    // This is a suggested workaround (by AGP or Gradle)
    lintOptions {
        abortOnError false
    }

    packagingOptions {
        //doNotStrip "*/*/*.so" // debuggable!
    }
}

dependencies {
    implementation project(':androidaudioplugin-lv2')
    implementation 'org.androidaudioplugin:androidaudioplugin:0.6.16'
    implementation 'org.androidaudioplugin:androidaudioplugin-ui-compose:0.6.16'
    implementation 'org.androidaudioplugin:androidaudioplugin-midi-device-service:0.6.16'
    implementation 'com.google.android.material:material:1.3.0'

    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'org.androidaudioplugin:androidaudioplugin-testing:0.6.16'
    androidTestImplementation 'androidx.test:rules:1.3.0'
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
}

// Starting AGP 7.0.0-alpha05, AGP stopped caring build dependencies and it broke builds.
// This is a forcible workarounds to build libandroidaudioplugin.so in prior to referencing it.
gradle.projectsEvaluated {
    tasks['buildCMakeDebug'].dependsOn(rootProject.project("androidaudioplugin-lv2").mergeDebugNativeLibs)
    // It is super awkward in AGP or Android Studio, but gradlew --tasks does not list
    // `buildCMakeRelease` while `buildCMakeDebug` is there!
    // Fortunately(?) we can rely on another awkwardness that we reference *debug* version of
    //  -;androidaudioplugin in CMakeLists.txt for target_link_directories() so we can skip
    //  release dependency here (technically we need debug build *always* happen before
    //  release builds, but it always seems to happen.
//    tasks['buildCMakeRelease'].dependsOn(rootProject.project("androidaudioplugin-lv2").mergeReleaseNativeLibs)
}
