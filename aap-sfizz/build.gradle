plugins {
    id 'com.android.library'
    id 'kotlin-android'
}

android {
    compileSdkVersion 30
    buildToolsVersion "30.0.0"

    defaultConfig {
        minSdkVersion 29
        targetSdkVersion 30
        versionCode 1
        versionName "1.0"

        externalNativeBuild {
            cmake {
                arguments "-DSFIZZ_JACK=off", "-DSFIZZ_STATIC_LIBSNDFILE=on"
            }
        }

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }

    externalNativeBuild {
        cmake {
            path "../dependencies/sfizz/CMakeLists.txt"
        }
    }
    ndkVersion = '21.0.6113669'

    packagingOptions {
        doNotStrip "*/*/*.so" // debuggable!
    }
}

dependencies {
    implementation project(':androidaudioplugin')
    implementation project(':androidaudioplugin-lv2')

    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'androidx.core:core-ktx:1.3.0'
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'com.google.android.material:material:1.1.0'
    testImplementation 'junit:junit:4.+'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
}

task copyLv2Manifests(type: Copy) {
    from(".cxx/cmake/debug/x86/sfizz.lv2") {
        exclude '**/*.so'
    }
    into 'src/main/assets/lv2/sfizz.lv2'
}

task deleteLV2Manifests(type: Delete) {
    delete 'src/main/assets/lv2/sfizz.lv2'
}

tasks.whenTaskAdded { task ->
    if (task.name == "bundleDebugAar" || task.name == "bundleReleaseAar")
        task.dependsOn copyLv2Manifests
    // FIXME: it's hacky, but `clean` didn't work.
    if (task.name == "externalNativeBuildCleanDebug" || task.name == "externalNativeBuildCleanRelease")
        task.dependsOn deleteLV2Manifests
}
